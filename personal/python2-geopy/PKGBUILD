# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jyantis <yantis@yantis.net>

pkgname=python2-geopy
pkgver=1.10.0
pkgrel=1
pkgdesc='Geocoding library for Python'
arch=('any')
url='https://github.com/geopy/geopy'
license=('BSD')
depends=('python2' 'python2-pytz' 'python2-requests-oauthlib')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/g/geopy/geopy-${pkgver}.tar.gz")
sha256sums=('e8d6a15542050af1426cf0f7f867df5ea3f453f337c292b9ff1243ba464c4231')

prepare() {
  cd geopy-${pkgver}

  for file in $(find . -name '*.py' -print); do
    sed -r -i 's_^#!.*/usr/bin/python(\s|$)_#!/usr/bin/python2_' $file
    sed -r -i 's_^#!.*/usr/bin/env(\s)*python(\s|$)_#!/usr/bin/env python2_' $file
  done
}

build() {
  cd geopy-${pkgver}

  python2 setup.py build
}

package() {
  cd geopy-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
