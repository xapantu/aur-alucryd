# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mike Swanson <mikeonthecomputer@gmail.com>
# Contributor: Themaister <maister@archlinux.us>

pkgname=retroarch-git
pkgver=1.3.4.r886.04c30c7
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('i686' 'x86_64')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
depends=('alsa-lib' 'freetype2' 'glibc' 'libdrm' 'libgl' 'libpulse'
         'libusb' 'libx11' 'libxext' 'libxinerama' 'libxkbcommon' 'libxv'
         'libxxf86vm' 'mesa' 'openal' 'python' 'sdl2' 'wayland' 'zlib'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'libswresample.so'
         'libswscale.so' 'libudev.so')
makedepends=('git')
optdepends=('libretro-bsnes: Super Nintendo Entertainment System cores'
            'libretro-desmume: Nintendo DS core'
            'libretro-gambatte: Nintendo Game Boy/Game Boy Color core'
            'libretro-genesis-plus: Sega Master System/Genesis/Game Gear/CD/32X core'
            'libretro-mgba: Nintendo Game Boy Advance core'
            'libretro-mupen64plus: Nintendo 64 core'
            'libretro-nestopia: Nintendo Entertainment System core'
            'libretro-pcsx-rearmed: Sony PlayStation core'
            'libretro-ppsspp: Sony PlayStation Portable core'
            'libretro-snes9x: Super Nintendo Entertainment System core'
            'libretro-yabause: Sega Saturn core'
            'libretro-overlays: Collection of overlays'
            'libretro-shaders: Collection of shaders')
provides=('retroarch')
conflicts=('retroarch')
backup=('etc/retroarch.cfg')
source=('git+https://github.com/libretro/RetroArch.git')
sha256sums=('SKIP')

pkgver() {
  cd RetroArch

  git describe --tags | sed 's/v//; s/-/.r/; s/-g/./'
}

build() {
  cd RetroArch

  ./configure \
    --prefix='/usr' \
    --disable-cg \
    --disable-jack \
    --disable-oss
  make
}

package() {
  cd RetroArch

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
