# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgbase=x264-git
pkgname=('x264-git' 'libx264-git' 'libx264-10bit-git')
pkgver=r2582.57031da
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
makedepends=('ffms2' 'git' 'l-smash' 'yasm')
source=('git://git.videolan.org/x264/x264-sandbox.git#branch=stable')
md5sums=('SKIP')

pkgver() {
  cd x264-sandbox

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cp -r x264-sandbox x264-sandbox-10bit
}

build() {
  cd x264-sandbox

  ./configure \
    --prefix='/usr' \
    --bit-depth='8' \
    --enable-shared \
    --disable-avs
  make

  cd ../x264-sandbox-10bit

  ./configure \
    --prefix='/usr' \
    --bit-depth='10' \
    --enable-shared \
    --disable-avs
  make
}

package_x264-git() {
  pkgdesc='CLI tools for encoding H264/AVC video streams.'
  depends=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'libffms2.so'
           'liblsmash.so')
  provides=('x264')
  conflicts=('x264')

  cd x264-sandbox

  make DESTDIR="${pkgdir}" install-cli

  cd ../x264-sandbox-10bit

  install -m 755 x264 "${pkgdir}"/usr/bin/x264-10bit
}

package_libx264-git() {
  pkgdesc='Library for encoding H264/AVC video streams.'
  depends=('glibc')
  provides=('libx264' 'libx264.so')
  conflicts=('libx264')

  cd x264-sandbox

  install -dm 755 "${pkgdir}"/usr/lib
  make DESTDIR="${pkgdir}" install-lib-{dev,shared}
}

package_libx264-10bit-git() {
  pkgdesc='Library for encoding H264/AVC video streams.'
  depends=('glibc')
  provides=('libx264' 'libx264.so')
  conflicts=('libx264')

  cd x264-sandbox-10bit

  install -dm 755 "${pkgdir}"/usr/lib
  make DESTDIR="${pkgdir}" install-lib-{dev,shared}
}

# vim: ts=2 sw=2 et:
