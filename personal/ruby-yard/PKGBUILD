# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=ruby-yard
pkgver=0.8.7.6
pkgrel=1
pkgdesc='Documentation tool for consistent and usable documentation in Ruby.'
arch=('any')
url='http://yardoc.org'
license=('MIT')
depends=('ruby')
makedepends=('rubygems')
options=('!emptydirs')
source=("http://gems.rubyforge.org/gems/yard-${pkgver}.gem")
noextract=("yard-${pkgver}.gem")
sha256sums=('3da10884b94fa25b7476f619b1d2cfb61290e10673965c545975630cd88a46b6')


package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} yard-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-yard
  ln -s ${_gemdir}/gems/yard-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/ruby-yard/
}

# vim: ts=2 sw=2 et:
