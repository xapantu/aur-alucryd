# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Olaf Bauer <hydro@freenet.de>

pkgname=makemkv
pkgver=1.9.5
pkgrel=1
pkgdesc='DVD and Blu-ray to MKV converter and network streamer'
arch=('i686' 'x86_64')
url='http://www.makemkv.com'
license=('LGPL' 'MPL' 'custom')
depends=('qt5-base' 'libxkbcommon-x11' 'hicolor-icon-theme' 'icu' 'ffmpeg')
optdepends_x86_64=('lib32-glibc: mmdtsdec')
install='makemkv.install'
source=("http://makemkv.com/download/makemkv-bin-${pkgver}.tar.gz"
        "http://makemkv.com/download/makemkv-oss-${pkgver}.tar.gz"
        'makemkv.1'
        'makemkvcon.1'
        'mmdtsdec.1')
sha256sums=('57468cc5d34fc442f2d4a7888e1baa2ec7f006027dc5ba3322472724636ef3e3'
            'cfd1729a40063dde2a52d3100091d2a0002aa616f87597108fa8d11c3ab5b5c6'
            '5573b2e4bade10d8cd258a7c235eb46f66ef8c8c97e5d5eb090c38fa0f94389b'
            'f12c0facf2f0071a9f728b138986f0a4c2b4ff6ace2dfb2e96364e215e9fda6f'
            '2a6237d3d5ce073734c658c7ec5d2141ecd0047e6d3c45d1bd594135c928878f')

build() {
  cd makemkv-oss-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd makemkv-oss-${pkgver}

  make DESTDIR="${pkgdir}" install

  cd ../makemkv-bin-${pkgver}

  install -dm 755 "${pkgdir}"/usr/{bin,share}
  if [[ $CARCH = 'x86_64' ]]; then
    install -m 755 bin/amd64/makemkvcon "${pkgdir}"/usr/bin/
  else
    install -m 755 bin/i386/makemkvcon "${pkgdir}"/usr/bin/
  fi
  install -m 755 bin/i386/mmdtsdec "${pkgdir}"/usr/bin/
  cp -dr --no-preserve='ownership' src/share "${pkgdir}"/usr/share/MakeMKV

  install -dm 755 "${pkgdir}"/usr/share/man/man1
  install -m 644 ../*.1 "${pkgdir}"/usr/share/man/man1/

  install -dm 755 "${pkgdir}"/usr/share/licenses/makemkv
  install -m 644 src/eula_en_linux.txt "${pkgdir}"/usr/share/licenses/makemkv/
}

# vim: ts=2 sw=2 et:
