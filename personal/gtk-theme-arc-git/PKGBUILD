# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: zach <zach@zach-adams.com>
# Contributor: Gordian Edenhofer <gordian.edenhofer@yahoo.de>

pkgname=gtk-theme-arc-git
pkgver=r289.b890581
pkgrel=1
pkgdesc='A flat theme with transparent elements'
arch=('any')
url='https://github.com/horst3180/Arc-theme'
license=('GPL3')
depends=('gtk3' 'gtk-engine-murrine')
makedepends=('git')
source=('gtk-theme-arc::git+https://github.com/horst3180/Arc-theme.git')
sha256sums=('SKIP')

pkgver() {
  cd gtk-theme-arc

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd gtk-theme-arc

  ./autogen.sh \
    --prefix='/usr'
}

package() {
  cd gtk-theme-arc

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
