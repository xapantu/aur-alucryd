# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx-git
pkgver=r942.46bb2e6
pkgrel=1
pkgdesc='Sega Master System/Genesis/Game Gear/CD/32X core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/Genesis-Plus-GX'
license=('BSD' 'LGPL2.1')
groups=('libretro')
depends=('glibc')
makedepends=('git')
provides=('libretro-genesis-plus-gx')
conflicts=('libretro-genesis-plus-gx')
source=('libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/genesis_plus_gx_libretro.info')
sha256sums=('SKIP'
            'a60fa12ccd583e5da2a42c7bf27b24c7b38e3e2bf4565ede07efe6bf087677ee')

pkgver() {
  cd libretro-genesis-plus-gx

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-genesis-plus-gx

  make -f Makefile.libretro
}

package() {
  cd libretro-genesis-plus-gx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 genesis_plus_gx_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../genesis_plus_gx_libretro.info "${pkgdir}"/usr/share/libretro/info/

  install -dm 755 "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx-git
  install -m 644 LICENSE.txt "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx-git/
}

# vim: ts=2 sw=2 et:
