# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

pkgbase=citra-git
pkgname=('citra-cli-git' 'citra-qt-git')
pkgver=r3437.0b7efc2
pkgrel=1
pkgdesc='A Nintendo 3DS emulator'
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
makedepends=('boost' 'cmake' 'desktop-file-utils' 'git' 'mesa' 'qt5-base'
             'sdl2' 'shared-mime-info')
source=("git+https://github.com/citra-emu/citra"
        'git+https://github.com/svn2github/inih'
        'git+https://github.com/neobrain/nihstro')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
install='citra.install'

pkgver() {
  cd citra

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd citra

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  git submodule init
  git config submodule.inih.url ../inih
  git config submodule.nihstro.url ../nihstro
  git submodule update
}

build() {
  cd citra/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package_citra-cli-git() {
  depends=('gcc-libs' 'glibc' 'libpng' 'sdl2')

  cd citra/build

  install -dm 755 "${pkgdir}"/usr/bin
  install -m 755 src/citra/citra "${pkgdir}"/usr/bin/
}

package_citra-qt-git() {
  depends=('desktop-file-utils' 'gcc-libs' 'glibc' 'libpng' 'qt5-base'
           'shared-mime-info')

  cd citra/build

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/bin/citra
}

# vim: ts=2 sw=2 et:
