# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: yaroslav <proninyaroslav@mail.ru>
# Contributor: jdarch <jda.cloud+archlinux@gmail.com>

pkgname=libudev0
pkgver=182
pkgrel=3
pkgdesc='Dynamic library to access udev device information (legacy)'
url='https://www.kernel.org/pub/linux/utils/kernel/hotplug/udev/udev.html'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('gperf' 'kmod' 'usbutils')
source=("https://www.kernel.org/pub/linux/utils/kernel/hotplug/udev-${pkgver}.tar.xz")
sha256sums=('adb8892f3b8e4b7163703ed4a545774721a3d55a1062de001f08c477c03d97ab')

prepare() {
   cd udev-${pkgver}

   autoreconf -fi
}

build() {
  cd udev-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/udev' \
    --sysconfdir='/etc' \
    --disable-gudev \
    --disable-introspection \
    --with-systemdsystemunitdir='/usr/lib/systemd/system'
  make LIBS='-lrt'
}

package() {
  cd udev-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,lib/{libudev.so,pkgconfig,udev,systemd},share}}
}

# vim: ts=2 sw=2 et:
