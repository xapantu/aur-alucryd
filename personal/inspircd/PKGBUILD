# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maxime Poulin <maxpoulin64@gmail.com>
# Contributor: Damian Nowak <damian.nowak@atlashost.eu>
# Contributor: Kyle Fuller <inbox@kylefuller.co.uk>
# Contributor: jibi <jibi@paranoici.org>

pkgname=inspircd
pkgver=2.0.21
pkgrel=1
pkgdesc='A modular C++ IRC daemon'
arch=('i686' 'x86_64')
url='http://www.inspircd.org/'
license=('GPL2')
depends=('perl')
makedepends=('libmariadbclient' 'sqlite3' 'libldap' 'geoip' 'gnutls' 'openssl' 'libgcrypt')
optdepends=('gnutls: m_ssl_gnutls'
            'libgcrypt: m_ssl_gnutls'
            'openssl: m_ssl_openssl'
            'libmariadbclient: m_mysql'
            'pcre: m_regex_pcre'
            'sqlite3: m_sqlite3'
            'libldap: m_ldapoper and m_ldapauth'
            'geoip: m_geoip')
install='inspircd.install'
source=("https://github.com/inspircd/inspircd/archive/v$pkgver.tar.gz"
        'inspircd.service')
sha256sums=('bc2f861d754754a108797699319186130ef7d909204eb56ab2c3b1ae80c9d6c5'
            'b59bbb46ed411ad4f39f04539ea3957f3d029c4a2f8994769eb775b17a804351')

build() {
  cd inspircd-${pkgver}

  ./configure \
    --enable-extras='m_geoip.cpp' \
    --enable-extras='m_ldapauth.cpp' \
    --enable-extras='m_ldapoper.cpp' \
    --enable-extras='m_mysql.cpp' \
    --enable-extras='m_regex_pcre.cpp' \
    --enable-extras='m_regex_posix.cpp' \
    --enable-extras='m_sqlite3.cpp'

  ./configure \
    --uid=0 \
    --prefix=/usr/lib/inspircd \
    --binary-dir=/usr/bin \
    --module-dir=/usr/lib/inspircd/modules \
    --config-dir=/etc/inspircd \
    --data-dir=/var/lib/inspircd \
    --log-dir=/var/log/inspircd \
    --enable-gnutls \
    --enable-openssl \
    --enable-epoll

  make
}

package() {
  install -Dm644 "${srcdir}"/inspircd.service "${pkgdir}"/usr/lib/systemd/system/inspircd.service
  install -o141 -g141 -dm750 "${pkgdir}/var/log/inspircd" "${pkgdir}/var/lib/inspircd"

  cd "${srcdir}/inspircd-${pkgver}"
  make DESTDIR="$pkgdir" install

  mkdir -p "${pkgdir}"/usr/share/inspircd
  mv "${pkgdir}"/etc/inspircd/examples "${pkgdir}"/usr/share/inspircd/examples

  rm -rf "${pkgdir}"/usr/lib/inspircd/logs
  rm -rf "${pkgdir}"/usr/lib/inspircd/data
}

# vim: ts=2 sw=2 et:
