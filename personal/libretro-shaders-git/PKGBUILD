# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-shaders-git
pkgver=r501.2b7edd6
pkgrel=1
pkgdesc='Collection of shaders for libretro'
arch=('any')
url='http://www.libretro.com/'
license=('')
groups=('libretro')
makedepends=('git' 'nvidia-cg-toolkit' 'python' 'retroarch')
provides=('libretro-shaders')
conflicts=('libretro-shaders')
source=('git+https://github.com/libretro/common-shaders.git')
sha256sums=('SKIP')

pkgver() {
  cd common-shaders

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  retroarch-cg2glsl common-shaders shaders
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' shaders "${pkgdir}"/usr/share/libretro/
}

# vim: ts=2 sw=2 et:
