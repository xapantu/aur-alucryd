# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server-devel
pkgver=3.0.5724.4
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=('i686' 'x86_64' 'armv6h')
url='http://emby.media'
license=('GPL2')
depends=('ffmpeg' 'imagemagick' 'libmediainfo' 'mono' 'sqlite')
provides=('emby-server')
conflicts=('emby-server')
install='emby-server.install'
_commit='30bf03041642acd4714c2111d2a6a60c064e5226'
source=("git+https://github.com/MediaBrowser/Emby.git#commit=${_commit}"
        'emby-server'
        'emby-server.conf'
        'emby-server.service')
backup=('etc/conf.d/emby-server')
sha256sums=('SKIP'
            '1515d8d61ba903ed8ca73c1930fb9f14e2acaa9c2b456d85fd82c2254558eb85'
            '064a82fb13816af5eb2a90bc8320d062ead0bf700715ee65df6afb347f890907'
            '6992ff2537e159e6e4efd3739b95e67e2e2bfcf6d3915564617062c34d8bfa9f')

prepare() {
  cd Emby

  sed 's/libMagickWand-6.Q8.so/libMagickWand-6.Q16HDRI.so/' -i MediaBrowser.Server.Mono/ImageMagickSharp.dll.config
}

build() {
  cd Emby

  xbuild \
    /p:Configuration='Release Mono' \
    /p:Platform='Any CPU' \
    /p:OutputPath="${srcdir}/build" \
    /t:build MediaBrowser.Mono.sln
}

package() {
  install -dm 755 "${pkgdir}"/{etc/conf.d,usr/{bin,lib/systemd/system}}
  cp -dr --no-preserve='ownership' build "${pkgdir}"/usr/lib/emby-server
  install -m 755 emby-server "${pkgdir}"/usr/bin/
  install -m 644 emby-server.service "${pkgdir}"/usr/lib/systemd/system/
  install -m 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server

  install -dm 755 "${pkgdir}"/var/lib/emby
  chown 422:422 -R "${pkgdir}"/var/lib/emby
}

# vim: ts=2 sw=2 et:
