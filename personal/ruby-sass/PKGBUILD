# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=ruby-sass
pkgver=3.4.21
pkgrel=1
pkgdesc='Tools and Ruby libraries for the CSS3 extension languages: SASS and SCSS.'
arch=('any')
url='http://sass-lang.com/'
license=('MIT')
depends=('ruby-yard' 'ruby-maruku')
makedepends=('rubygems')
options=('!emptydirs')
source=("http://gems.rubyforge.org/gems/sass-${pkgver}.gem")
noextract=("sass-${pkgver}.gem")
sha256sums=('05f907ae3bde56e0133298295ff0befcfec2cb29799ef4f40236eb7abb5173d2')


package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} -n "${pkgdir}"/usr/bin sass-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-sass
  ln -s ${_gemdir}/gems/sass-${pkgver}/MIT-LICENSE "${pkgdir}"/usr/share/licenses/ruby-sass/
}

# vim: ts=2 sw=2 et:
