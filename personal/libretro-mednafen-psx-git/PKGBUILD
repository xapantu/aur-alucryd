# Maintainer: Maxime Gauduin <alucryd@Ã¦rchlinux.org>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgbase=libretro-mednafen-psx-git
pkgname=('libretro-mednafen-psx-git' 'libretro-mednafen-psx-hw-git')
pkgver=r1139.1a605d0
pkgrel=1
pkgdesc='Sony playStation core'
arch=('i686' 'x86_64')
url="https://github.com/libretro/beetle-psx-libretro"
license=('GPLv2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'libgl' 'mesa')
source=('libretro-mednafen-psx::git+https://github.com/libretro/beetle-psx-libretro.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_libretro.info'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_hw_libretro.info')
sha256sums=('SKIP'
            '8517d3ddd709d6ffec1b41b2d05d91d0f60ba434ac2b029d834db405960e0a7d'
            'aef5dc6381a382ea062132bc4dde322b619576a0975722327a1e0785b87ab4d6')

pkgver() {
  cd libretro-mednafen-psx

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd libretro-mednafen-psx

  make
  make HAVE_OPENGL=1
}

package_libretro-mednafen-psx-git() {
  provides=('libretro-mednafen-psx')
  conflicts=('libretro-mednafen-psx')

  cd libretro-mednafen-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

package_libretro-mednafen-psx-hw-git() {
  depends+=('libgl')
  provides=('libretro-mednafen-psx-hw')
  conflicts=('libretro-mednafen-psx-hw')

  cd libretro-mednafen-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_hw_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_hw_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
