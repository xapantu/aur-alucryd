# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jesse Jaara <jesse.jaara@gmail.com>

pkgname=libpng12
pkgver=1.2.53
pkgrel=3
pkgdesc='A collection of routines used to create PNG format graphics files'
arch=('i686' 'x86_64')
url='http://www.libpng.org/pub/png/libpng.html'
license=('custom')
depends=('zlib')
source=("http://sourceforge.net/projects/libpng/files/libpng12/${pkgver}/libpng-${pkgver}.tar.xz"
        "http://sourceforge.net/projects/apng/files/libpng/libpng12/libpng-${pkgver}-apng.patch.gz")
sha256sums=('b45e49f689e7451bd576569e6a344f7e0d11c02ecbb797f4da0e431526765c0a'
            '64745f978fd9419a533942181316ddd1cafce63e899bc572f37d6614831c8ed3')

prepare(){
  cd libpng-${pkgver}

  patch -Np0 -i ../libpng-${pkgver}-apng.patch

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
}

build() {
  cd libpng-${pkgver}

  ./configure \
    --prefix=/usr
  make
}

package() {
  cd libpng-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,lib/{libpng.{a,so},pkgconfig},share}

  install -dm 755 "${pkgdir}"/usr/share/licenses/libpng12
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/libpng12/
}

# vim: ts=2 sw=2 et:
