# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tom < tomgparchaur at gmail dot com >
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dropbox
pkgver=3.10.9
pkgrel=1
pkgdesc='Keep your files safe, synced, and easy to share'
arch=('i686' 'x86_64')
url='http://www.dropbox.com'
license=('custom')
depends=('libxmu' 'libxslt')
makedepends=('chrpath')
optdepends=('ufw-extras: UFW rules for Dropbox'
            'perl-file-mimeinfo: Open the Dropbox folder on some DEs')
options=('!strip')
source=('dropbox.desktop'
        'dropbox.png'
        'dropbox.service'
        'dropbox@.service'
        'terms.txt')
source_i686=("https://dl.dropboxusercontent.com/u/17/dropbox-lnx.x86-${pkgver}.tar.gz")
source_x86_64=("https://dl.dropboxusercontent.com/u/17/dropbox-lnx.x86_64-${pkgver}.tar.gz")
sha256sums=('3daf27f16b3f11a435def3d2f4bb06702d330b01c35e46e07d8f01fa97070b36'
            'e7d245f5d1a3d5322614b61400ae2913a8caef44bc86717ff7d8197a15dd7f01'
            '5ea25085d97708d9c9ffe3d0754dab3e09937ba8111a80b67e443dae3c15503e'
            '85ccabbe4e1f2dc67a480bc074d949578b148168a656dcb19860bb9c4f7b3e8a'
            'c39d63bcfc0d40002a5df052298feb79f2e8dd3d93da27ef31eb672b8d2fc70e')
sha256sums_i686=('e9b8eebbcbdc755bd79443bf9177d51ea7b077d6407593252dbc08d28f4b5d9c')
sha256sums_x86_64=('4944ffbf845ee8b258641181b0612f8b598e53f9b113434ad0c3b8887ee8e6cd')

prepare() {
  cd .dropbox-dist/dropbox-lnx.*-${pkgver}

  rm library.zip
  ln -s dropbox library.zip
  chrpath -d plugins/platforms/libqxcb.so
}

package() {
  cd .dropbox-dist

  install -dm 755 "${pkgdir}"/opt
  cp -dr --no-preserve='ownership' dropbox-lnx.*-${pkgver} "${pkgdir}"/opt/dropbox
  find "${pkgdir}"/opt/dropbox -type d -exec chmod 755 {} +
  find "${pkgdir}"/opt/dropbox -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/opt/dropbox/dropbox{,d}

  install -dm 755 "${pkgdir}"/usr/bin
  ln -s /opt/dropbox/dropbox "${pkgdir}"/usr/bin/

  install -dm 755 "${pkgdir}"/usr/share/{applications,licenses/dropbox,pixmaps}
  install -m 644 ../dropbox.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../dropbox.png "${pkgdir}"/usr/share/pixmaps/
  install -m 644 ../terms.txt "${pkgdir}"/usr/share/licenses/dropbox/

  install -dm 755 "${pkgdir}"/usr/lib/systemd/{user,system}
  install -m 644 ../dropbox.service "${pkgdir}"/usr/lib/systemd/user/
  install -m 644 ../dropbox@.service "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
