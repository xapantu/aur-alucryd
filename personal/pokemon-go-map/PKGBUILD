# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Grey Christoforo <grey@christoforo.net>

pkgname=pokemon-go-map
pkgver=2.1.0.develop
pkgrel=1
pkgdesc='Live visualization of all the pokemon in your area... and more!'
arch=('any')
url='https://jz6.github.io/PoGoMap/'
license=('AGPL3')
depends=('python2-configargparse' 'python2-click' 'python2-flask'
         'python2-flask-cache-bust' 'python2-flask-cors'
         'python2-flask-compress' 'python2-itsdangerous' 'python2-jinja'
         'python2-latlon' 'python2-markupsafe' 'python2-peewee'
         'python2-pgoapi' 'python2-pymysql' 'python2-recommonmark'
         'python2-sphinx' 'python2-sphinx-autobuild' 'python2-sphinx_rtd_theme'
         'python2-werkzeug' 'python2-wsgiref' 'python2-xxhash')
makedepends=('git')
install='pokemon-go-map.install'
source=("git+https://github.com/PokemonGoMap/PokemonGo-Map.git#branch=develop"
        'pokemon-go-map'
        'pokemon-go-map.service')
sha256sums=('SKIP'
            '0ccd6ed4c003fc1fa042f6d7faf77fcc40d12907a123a50e19d5c74ed12952e8'
            '0facaaefa7193f31562c4c9ecd7a7fd88f8187040d969f135074fa7b20e49700')

package() {
  install -dm 755 "${pkgdir}"/{usr/{bin,lib/systemd/system},opt}
  cp -dr --no-preserve='ownership' PokemonGo-Map "${pkgdir}"/opt/pokemon-go-map
  install -m 755 pokemon-go-map "${pkgdir}"/usr/bin/pokemon-go-map
  install -m 644 pokemon-go-map.service "${pkgdir}"/usr/lib/systemd/system/

  install -dm 755 "${pkgdir}"/var/lib/pokemon-go-map
  chown 721:721 "${pkgdir}"/var/lib/pokemon-go-map
}

# vim: ts=2 sw=2 et:
