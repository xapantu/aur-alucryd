# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=vapoursynth-plugin-f3kdb-git
pkgver=2.0.r412.9f3042e
pkgrel=1
pkgdesc='flash3kyuu deband plugin for VapourSynth'
arch=('i686' 'x86_64')
url='https://github.com/SAPikachu/flash3kyuu_deband'
license=('GPL3')
depends=('vapoursynth')
makedepends=('git')
provides=('vapoursynth-plugin-f3kdb')
conflicts=('vapoursynth-plugin-f3kdb')
source=('vapoursynth-plugin-f3kdb::git+https://github.com/SAPikachu/flash3kyuu_deband.git')
sha256sums=('SKIP')

pkgver() {
  cd vapoursynth-plugin-f3kdb

  printf "2.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd vapoursynth-plugin-f3kdb

  ./waf configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  ./waf build
}

package() {
  cd vapoursynth-plugin-f3kdb

  ./waf install \
    --destdir="${pkgdir}" \
    --no-ldconfig
}

# vim: ts=2 sw=2 et:
