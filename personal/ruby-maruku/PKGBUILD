# Maintainer: Maxime Gauduin <zlucryd@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Mario Finelli <mario.finelli@yahoo.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: oliparcol <oliparcol@gmail.com>

pkgname=ruby-maruku
pkgver=0.7.2
pkgrel=3
pkgdesc='Maruku is a Markdown-superset interpreter written in Ruby.'
arch=('any')
url='http://github.com/bhollis/maruku'
license=('MIT')
depends=('ruby')
makedepends=('rubygems')
options=('!emptydirs')
source=("https://rubygems.org/downloads/maruku-${pkgver}.gem")
noextract=("maruku-${pkgver}.gem")
sha256sums=('e8a755d8e9c24f1601157c8b376f0378f88d487fa777aa655f332504557a0798')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} maruku-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-maruku
  ln -s ${_gemdir}/gems/maruku-${pkgver}/MIT-LICENSE.txt "${pkgdir}"/usr/share/licenses/ruby-maruku/
}

# vim: ts=2 sw=2 et:
