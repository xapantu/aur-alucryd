# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-lsmashsource-git
pkgver=r875.14b040e
pkgrel=1
pkgdesc='L-SMASH source plugin for Vapoursynth'
arch=('i686' 'x86_64')
url='https://github.com/VFR-maniac/L-SMASH-Works'
license=('GPL')
depends=('glibc' 'vapoursynth'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
         'libswscale.so')
makedepends=('git')
provides=('vapoursynth-plugin-lsmashsource')
conflicts=('vapoursynth-plugin-lsmashsource')
source=('vapoursynth-plugin-lsmashsource::git+https://github.com/VFR-maniac/L-SMASH-Works.git')
sha256sums=('SKIP')

pkgver() {
  cd vapoursynth-plugin-lsmashsource

  echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd vapoursynth-plugin-lsmashsource/VapourSynth

  ./configure \
    --prefix='/usr'
  make
}

package(){
  cd vapoursynth-plugin-lsmashsource/VapourSynth

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
