# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Boohbah <boohbah@gmail.com>
# Contributor: bzt <unmacaque@gmail.com>

pkgbase=snes9x-git
pkgname=('snes9x-git' 'snes9x-gtk-git')
pkgver=1.53.r281.dfe3925
pkgrel=1
pkgdesc='A SNES emulator'
arch=('i686' 'x86_64')
url='http://www.snes9x.com/'
license=('custom' 'GPL2' 'LGPL2.1')
depends=('gcc-libs' 'libpng' 'libx11' 'libxext' 'zlib')
makedepends=('git' 'gtk3' 'intltool' 'libpulse' 'libsm' 'libxv' 'mesa' 'nasm'
             'portaudio' 'sdl')
source=('git://github.com/snes9xgit/snes9x.git')
sha256sums=('SKIP')

pkgver() {
  cd snes9x

  git describe --long | sed 's/-/.r/; s/-g/./'
}

build() {
  cd snes9x/unix

  autoreconf -fi
  ./configure \
    --prefix='/usr' \
    --enable-netplay
  make

  cd ../gtk

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --with-gtk3 \
    --with-netplay
  make
}

package_snes9x-git() {
  depends+=('libice' 'libsm')
  provides=('snes9x')
  conflicts=('snes9x')

  cd snes9x/unix

  install -dm 755 "${pkgdir}"/usr/bin
  install -m 755 snes9x "${pkgdir}"/usr/bin/

  install -dm 755 "${pkgdir}"/usr/share/doc/snes9x
  install -m 644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} "${pkgdir}"/usr/share/doc/snes9x/

  install -dm 755 "${pkgdir}"/usr/share/licenses/snes9x-git
  install -m 644 ../docs/snes9x-license.txt "${pkgdir}"/usr/share/licenses/snes9x-git/
}

package_snes9x-gtk-git() {
  depends+=('alsa-lib' 'cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2'
            'gtk3' 'hicolor-icon-theme' 'libgl' 'libpulse' 'libxml2'
            'libxrandr' 'libxv' 'pango' 'portaudio' 'sdl')
  provides=('snes9x-gtk')
  conflicts=('snes9x-gtk')
  install='snes9x.install'

  cd snes9x/gtk

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/doc/snes9x-gtk
  install -m 644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} "${pkgdir}"/usr/share/doc/snes9x-gtk/

  install -dm 755 "${pkgdir}"/usr/share/licenses/snes9x-gtk-git
  install -m 644 ../docs/snes9x-license.txt "${pkgdir}"/usr/share/licenses/snes9x-gtk-git/
}

# vim: ts=2 sw=2 et:
