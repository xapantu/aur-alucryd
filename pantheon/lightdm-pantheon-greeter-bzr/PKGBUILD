# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter-bzr
pkgver=r438
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-greeter'
license=('GPL')
groups=('pantheon-unstable')
depends=('cairo' 'clutter' 'clutter-gtk' 'cogl' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gtk3' 'libgee' 'libgl' 'libx11' 'lightdm' 'pango'
         'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('bzr' 'cmake' 'libxfixes' 'vala')
provides=('lightdm-pantheon-greeter')
conflicts=('lightdm-pantheon-greeter')
install='lightdm-pantheon-greeter.install'
source=('lightdm-pantheon-greeter::bzr+lp:pantheon-greeter'
        'lightdm-pantheon-greeter-paths.patch')
sha256sums=('SKIP'
            '8246073b9a242d8b2a097845af8aa8b33f8c0ba4c6815342b51a1f9c13b1afa8')

pkgver() {
  cd lightdm-pantheon-greeter

  echo "r$(bzr revno)"
}

prepare() {
  cd lightdm-pantheon-greeter
  bzr merge lp:~l-admin-3/pantheon-greeter/indicator-api-integration

  patch -Np1 -i ../lightdm-pantheon-greeter-paths.patch
}

build() {
  cd lightdm-pantheon-greeter

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd lightdm-pantheon-greeter/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
