# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=noise-player-bzr
pkgver=r1830
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=('i686' 'x86_64')
url='https://launchpad.net/noise'
license=('GPL3')
depends=('desktop-file-utils' 'granite-bzr' 'gst-plugins-base-libs' 'libgpod'
         'libnotify' 'libpeas' 'sqlheavy' 'taglib')
makedepends=('bzr' 'cmake' 'gobject-introspection' 'vala')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'gst-libav: Libav plugin')
replaces=('noise-bzr')
provides=('noise' 'noise-player')
conflicts=('noise' 'noise-player')
install='noise-player.install'
source=('bzr+lp:noise'
        'mesa-demos-conflict.patch'
        'noise-install-dirs.patch')
sha256sums=('SKIP'
            'b3bec8516d9497ee961fcd607e96b7c17ed76eed3d7af4093827ff5e052d0dc0'
            'd0ebc710a742d942e04a8df61d91dd75ae4b2aeafe7fece08f1fbc90c507f1df')

pkgver() {
  cd noise

  echo "r$(bzr revno)"
}

prepare() {
  cd noise

  patch -Np1 -i ../mesa-demos-conflict.patch
  patch -Np1 -i ../noise-install-dirs.patch

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd noise/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='FALSE'
  make -j1
}

package() {
  cd noise/build

  make -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
