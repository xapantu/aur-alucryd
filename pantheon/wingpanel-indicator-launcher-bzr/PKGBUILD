# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-launcher-bzr
pkgver=r2
pkgrel=2
pkgdesc='Launcher indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://code.launchpad.net/~parnold-x/+junk/wingpanel-indicator-launcher'
license=('GPL3')
groups=('pantheon-unstable')
depends=('glib2' 'glibc' 'gtk3' 'slingshot-launcher'
         'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('bzr' 'cmake' 'vala')
install='wingpanel-indicator-launcher.install'
source=('bzr+lp:~parnold-x/+junk/wingpanel-indicator-launcher')
sha256sums=('SKIP')

pkgver() {
  cd wingpanel-indicator-launcher

  echo "r$(bzr revno)"
}

prepare() {
  cd wingpanel-indicator-launcher

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd wingpanel-indicator-launcher/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'\
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd wingpanel-indicator-launcher/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
